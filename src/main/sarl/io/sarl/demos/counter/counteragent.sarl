package io.sarl.demos.counter

import io.sarl.core.Behaviors
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Schedules
import java.util.concurrent.atomic.AtomicInteger
import io.sarl.core.Logging

event Hello

event Count

event Die

agent CounterAgent {

	uses Lifecycle, Schedules, DefaultContextInteractions, Behaviors, Logging

	val count = new AtomicInteger

	var started = false
 
	on Initialize {
		loggingName = "COUNTER"
		info("Starting")
		task("discovery").every(1000) [ 
			emit(new Hello)
		]
	}

	on Hello [ occurrence.source != defaultAddress && !started ] {
		info("Found another agent => " + occurrence.source)
		info("I'm " + defaultAddress)
		started = true
		task("discovery").cancel
		info("Starting to count")
		wake(new Count) 
	}

	on Count {
		info("count => " + count.get) 
		if(count.get == 3) {
			info("Killing") 
			emit(new Die) 
			killMe
		} else {
			in(1000) [ 
				count.incrementAndGet 
				wake(new Count) 
			]
		}
	}

}

agent SecondAgent {

	uses Lifecycle, DefaultContextInteractions, Logging
	
	on Initialize {
		loggingName = "PARTNER"
		info("Starting")
	}

	on Hello [ occurrence.source != defaultAddress ] {
		info("Say hello!")
		emit(new Hello) 
	}

	on Die {
		info("Killing")
		killMe
	}

}
