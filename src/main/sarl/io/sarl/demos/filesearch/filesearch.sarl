package io.sarl.demos.filesearch


import io.sarl.core.Initialize
import io.sarl.core.Destroy
import io.sarl.core.Behaviors
import io.sarl.core.Lifecycle
import io.sarl.core.ExternalContextAccess
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.InnerContextAccess
import io.sarl.core.Schedules
import io.sarl.core.AgentSpawned
import io.sarl.core.AgentKilled
import io.sarl.core.MemberJoined
import io.sarl.core.MemberLeft
import io.sarl.core.ContextJoined
import io.sarl.core.ContextLeft
import static java.lang.String.*
import java.util.Date
import java.io.File
import io.sarl.lang.core.Event
import io.sarl.lang.core.Address
import io.sarl.util.AddressScope

/* Search for a given minSize (in kb) */
event Search {
	var fileMatch : String
	var date : Date
}

/* Notify a file matching the criteria was found */
event FoundFile {
	var file : String
}

/* Request the agent to stop processing */
event Stop{
	
}




agent SearchCommanderAgent {
	uses Behaviors, Lifecycle, DefaultContextInteractions, InnerContextAccess,Schedules
	on Initialize {
		emit(new Search=>[fileMatch=".sarl";date=new Date()])
		println("SearchCommander initialized")
	}
	
	on FoundFile{
		println(format("[receiver = %s, source=%s] Matching File Found [path=%s]",ID, occurrence.source.UUID,occurrence.file))		
	}
	
	on Destroy {
		println("Commander I'm about to die, bye :-)")
	}
}

/*
 * An Agent that searches for files larger than a given size.
 * 
 * To explore sub-directories this agent spawns members (sub-holons) in its innerContext.
 * 
 * This agent can be used to search in distant machines by setting the stayAlive and requesting a
 * with Search event in the default context.
 * 
 * @param String path where this agent looks for
 * @param Integar size the minimum size to match
 * @param Boolean stayAlive after search
 */
agent FileSearchAgent {
	uses Behaviors, Lifecycle, DefaultContextInteractions, InnerContextAccess,Schedules

	var myPath : String
	var stayAlive : Boolean
	var cmdAddr : Address
	
	on Initialize {  
		
		myPath = occurrence.parameters.get(0) as String
		//wake(new Search =>[minSize=new Integer(occurrence.parameters.get(1) as String)])
		stayAlive = Boolean.parseBoolean(occurrence.parameters.get(1) as String);
		println("FileSearch Launched in "+myPath)
	}
	
	on Search{
		val maxDate = occurrence.date
		val fMatch = occurrence.fileMatch
		cmdAddr = occurrence.source
		
		println(format("[ID=%s - parent %s] Searching in %s file matching %s created before %s", ID, defaultContext.ID,myPath,fMatch,maxDate.toString))
		
		
		val dir = new File(myPath)
		if(!dir.exists){
			throw new IllegalArgumentException("Path ["+myPath+"] is not a Directory")
		}
		
		for(f : dir.listFiles()){
			if(f.directory){
				if(!f.name.equals(".")&& !f.name.equals("..")) {
					val targetID = FileSearchAgent.spawnInContext(innerContext,#[f.absolutePath,false.toString]);
					innerContext.defaultSpace.emit(new Search =>[source=innerContext.defaultSpace.getAddress(ID); date=maxDate;fileMatch = fMatch], AddressScope.getScope(innerContext.defaultSpace.getAddress(targetID)));
				}
			}else{
				if((maxDate.time >= f.lastModified) && !(f.name.startsWith(".")) && (f.name.endsWith(fMatch))){
					emit(new FoundFile =>[file = f.absolutePath],AddressScope.getScope(cmdAddr))
				}
			}
		}
		
		if(!stayAlive){
			//wait for members searching in sub-directories
			every(500)[if(!hasMembers){wake(new Stop)}]
		}
	}
	
	on FoundFile{
		println(format("[receiver = %s, source=%s] Matching File Found [path=%s]",ID, occurrence.source.UUID,occurrence.file))	
		emit(occurrence, AddressScope.getScope(cmdAddr));	
		
	}
	
	on Stop{
		println(format("[ID=%s] Request to stop received", ID, occurrence.source.UUID))
		killMe
	}
	
	on Destroy {
		println("I'm about to die, bye :-)")
	}
	
	def isInternal(e:Event) : boolean {
		return e.source.spaceId.equals(innerContext.defaultSpace.ID);
	}
	
	def hasMembers : boolean {
		return innerContext.defaultSpace.participants.size > 1
	}
	
	
	on ContextJoined {
		println("ContextJoined, contextID:"+occurrence.holonContextID+" SpaceID: "+occurrence.defaultSpaceID)
	}
	
	on ContextLeft {
		println("ContextLeft, contextID:"+occurrence.holonContextID)
	}
	
	on MemberJoined {
		println("MemberJoined, agentID:"+occurrence.agentID)
	}
	
	on MemberLeft {
		println("MemberLeft, agentID:"+occurrence.agentID)		
	}
	
	on AgentSpawned {
		println("AgentSpawned, agentID:"+occurrence.agentID+" AgentType: "+occurrence.agentType)		
	}
	
	on AgentKilled {
		println("AgentKilled, agentID:"+occurrence.agentID)	
	}
	
	on Destroy {
		println("Destroy")	
	}
	
}