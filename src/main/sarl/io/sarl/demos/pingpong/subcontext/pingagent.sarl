package io.sarl.demos.pingpong.subcontext

import io.sarl.core.ExternalContextAccess
import io.sarl.core.Initialize
import io.sarl.core.Logging
import io.sarl.core.Schedules
import io.sarl.lang.core.AgentContext
import io.sarl.util.Scopes
import java.util.UUID

agent PingAgent {

	uses ExternalContextAccess, Schedules, Logging

	var context : AgentContext

	on Initialize {
		var cID = occurrence.parameters.get(0) as UUID
		var sID = occurrence.parameters.get(1) as UUID
		join(cID, sID)
		context = getContext(cID)

		info("Waiting for Pong Agent")
		val task = task("waiting_for_partner")
		task.every(1000) [
			if (context.defaultSpace.participants.size > 1) {
				info("Pong Agent detected.")
				info("Sending Ping #0")
				var evt = new Ping(0)
				evt.source = context.defaultSpace.getAddress(getID())
				context.defaultSpace.emit( evt )
				task.cancel
			}
		]
	}

	on Pong {
		info("Receiving Pong #" + occurrence.index)
		info("Sending Ping #" + (occurrence.index + 1))
		var evt = new Ping( occurrence.index + 1 )
		evt.source = context.defaultSpace.getAddress(getID())
		context.defaultSpace.emit(
			evt,
			Scopes.addresses( occurrence.source )
		)
	}

}
