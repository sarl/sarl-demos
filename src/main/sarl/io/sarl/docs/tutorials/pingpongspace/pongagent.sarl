package io.sarl.docs.tutorials.pingpongspace

import io.sarl.core.DefaultContextInteractions
import io.sarl.util.Scopes
import io.sarl.util.OpenEventSpace
import io.sarl.core.Initialize
import io.sarl.util.OpenEventSpaceSpecification
import io.sarl.core.Behaviors
import java.util.UUID
import io.sarl.core.Logging

agent PongAgent {
	
	uses DefaultContextInteractions, Behaviors, Logging
	
	var comspace : OpenEventSpace

	on Initialize {
		comspace = defaultContext.getOrCreateSpaceWithSpec(
			typeof(OpenEventSpaceSpecification),
			occurrence.parameters.get(0) as UUID)
		comspace.register(asEventListener())
	}
	
	on Ping {
		info("Receiving Ping #" + occurrence.index)
		info("Sending Pong #" + occurrence.index)
		var evt = new Pong( occurrence.index )
		evt.source = comspace.getAddress(getID)
		comspace.emit(
			evt,
			Scopes.addresses( occurrence.source )
		)
		
	}
}
